(def! load-file 
  (fn* (f) 
       (eval 
        (read-string 
         (str "(do " (slurp f) "\nnil)")))))

(defn! zero? [num] (if (= num 0) true false))

(defn! inc [num] (+ num 1))

(defn! dec [num] (- num 1))

(defn! even? [num] (zero? (mod num 2)))

(defn! odd? [num] (not (even? num)))

(defn! reduce [func context sequence]
  (if (empty? sequence) 
    context 
    (reduce func (func context (first sequence)) (rest sequence))))

(defn! map [func sequence]
  (let* [new-fun (fn* [context element] (cons (func element) context))]
        (reverse (reduce new-fun (list) sequence))))

(defn! filter [func sequence]
  (let* [new-fun (fn* [context element] 
                      (if (func element) (cons element context) context))] 
        (reverse (reduce new-fun (list) sequence))))

(defn! some? [value]
  (if (= nil value) false true))

(defn! some [func sequence]
  (let* [remaining-elements (rest sequence)]
        (cond (func (first sequence)) true
              (empty? remaining-elements) nil
              :else (some func remaining-elements))))